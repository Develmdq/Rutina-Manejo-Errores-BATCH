      ******************************************************************
       IDENTIFICATION DIVISION.
       PROGRAM-ID. RUTERRBA.

      *>**
      *= RUTINA DE MANEJO DE ERRORES PARA PROGRAMAS BATCH EN COBOL
      *- @autor:        Eduardo Marcet
      *- @fecha:        2026-01-15
      *- @version:      1.0
      *- @licencia:     MIT
      *- @modificacion: 2026-05-28
      *- @change: 2026-01-15 EMarcet Creacion inicial del programa
      *-
      *-* FUNCION
      *  RECIBE LA ESTRUCTURA DE ERROR DEL PROGRAMA LLAMADOR y MUESTRA
      *  UN BLOQUE INFORMATIVO EN EL SPOOL PARA EL OPERADOR DE TURNO
      *-
      *-* USO DESDE EL PROGRAMA LLAMADOR
      *
      *   1. INCLUIR LA COPY EN WORKING-STORAGE:   COPY CPERROR.
      *
      *   2. ANTES DE INVOCAR LA RUTINA, CARGAR LOS CAMPOS
      *      RELEVANTES DE WS-ERROR SEGUN EL CONTEXTO DEL ERROR:
      *      MOVE 'MIPGM001' TO WS-ERR-PROGRAMA
      *      MOVE SQLCA     TO WS-ERR-SQLCA
      *      (u otros campos segun corresponda)
      *
      *   3. INVOCAR LA RUTINA DINAMICAMENTE:
      *      CALL WS-RUTERRBA USING WS-ERROR
      *-
      *>**

      ******************************************************************
       ENVIRONMENT DIVISION.
      ******************************************************************
       CONFIGURATION SECTION.
       SPECIAL-NAMES.
                 DECIMAL-POINT IS COMMA.

      ******************************************************************
       DATA DIVISION.
      ******************************************************************
       WORKING-STORAGE SECTION.
       77 FILLER               PIC X(26)    VALUE '* INICIO WS *'.

       01 WS-SEPARADOR         PIC X(60)    VALUE ALL '='.
       01 WS-SEPARADOR-MIN     PIC X(60)    VALUE ALL '-'.
       01 WS-ENCABEZADO        PIC X(60)    VALUE SPACES.

      * FECHA Y HORA DEL ERROR *
       01 WS-FECHA-HORA-ERR.
             05 WS-AAAA        PIC X(04).
             05 WS-MM          PIC X(02).
             05 WS-DD          PIC X(02).
             05 WS-HH          PIC X(02).
             05 WS-MIN         PIC X(02).
             05 WS-SS          PIC X(02).

      * BUFFER PARA DSNTIAR *
       01 WS-DSNTIAR-MSG.
           05 WS-DSNTIAR-LONG  PIC S9(04)   COMP VALUE +288.
           05 WS-DSNTIAR-TEXT  PIC X(72)    OCCURS 4 TIMES.

       01 WS-ERROR-TEXT-LEN    PIC S9(9)    COMP VALUE +72.

       77 FILLER               PIC X(26)    VALUE '* FINAL  WS *'.

       LINKAGE SECTION.

           COPY CPERROR.

      ******************************************************************
       PROCEDURE DIVISION USING WS-ERROR.
      ******************************************************************
       MAIN-PROGRAM.
           PERFORM 1000-I-INICIO    THRU 1000-F-INICIO
           PERFORM 2000-I-PROCESO   THRU 2000-F-PROCESO
           PERFORM 3000-I-FINAL     THRU 3000-F-FINAL
           .
       F-MAIN-PROGRAM. GOBACK.

      ******************************************************************
      *              CAPTURAR FECHA Y HORA DEL ERROR                   *
      ******************************************************************
       1000-I-INICIO.
           MOVE FUNCTION CURRENT-DATE(1:14)  TO WS-FECHA-HORA-ERR
           .
       1000-F-INICIO.  EXIT.

       2000-I-PROCESO.

           MOVE SPACES TO WS-ENCABEZADO
           STRING '>>> INFORME DE ERROR      ' DELIMITED BY SIZE
                  WS-DD                        DELIMITED BY SIZE
                  '/'                          DELIMITED BY SIZE
                  WS-MM                        DELIMITED BY SIZE
                  '/'                          DELIMITED BY SIZE
                  WS-AAAA                      DELIMITED BY SIZE
                  ' - '                        DELIMITED BY SIZE
                  WS-HH                        DELIMITED BY SIZE
                  ':'                          DELIMITED BY SIZE
                  WS-MIN                       DELIMITED BY SIZE
                  '     <<< '                  DELIMITED BY SIZE
             INTO WS-ENCABEZADO
           END-STRING
           .
       2000-F-PROCESO.  EXIT.

       3000-I-FINAL.

           DISPLAY WS-ENCABEZADO
           DISPLAY WS-SEPARADOR-MIN
           DISPLAY 'PROGRAMA     : ' WS-ERR-PROGRAMA
           DISPLAY WS-SEPARADOR-MIN
           DISPLAY 'TIPO ERROR   : ' WS-TIPO-ERROR
           DISPLAY WS-SEPARADOR-MIN

           EVALUATE TRUE
              WHEN WS-ERR-FS NOT = '00'
                 DISPLAY 'FILE STATUS  : ' WS-ERR-FS
              WHEN WS-ERR-SQLCODE < 0
                    CALL 'DSNTIAR' USING WS-ERR-SQLCA
                                         WS-DSNTIAR-MSG
                                         WS-ERROR-TEXT-LEN
                    DISPLAY WS-DSNTIAR-TEXT(1)
                    DISPLAY WS-DSNTIAR-TEXT(2)
                    DISPLAY WS-DSNTIAR-TEXT(3)
                    DISPLAY WS-DSNTIAR-TEXT(4)
           END-EVALUATE
           DISPLAY WS-SEPARADOR
           .
        3000-F-FINAL.  EXIT.

